drip mergeSort(lst: List[INT]) => List[INT]:
    List[INT] sortedList = [];
    List[INT] aux = [];
    INT width;
    INT n_len;

    INT i = 0;
    while (i < len(lst)):
        append(sortedList, lst[i]);
        append(aux, lst[i]);
        i = i + 1;
    ;

    width = 1;
    n_len = len(sortedList);

    while (width < n_len):
        INT i = 0;

        while (i < n_len):
            INT left = i;
            INT mid = left + width;
            INT right = left + 2 * width;

            if (mid > n_len):
                mid = n_len;
            ;

            if (right > n_len):
                right = n_len;
            ;

            INT l = left;
            INT r = mid;
            INT m = left;

            while (l < mid && r < right):
                if (sortedList[l] <= sortedList[r]):
                    aux[m] = sortedList[l];
                    l = l + 1;
                else:
                    aux[m] = sortedList[r];
                    r = r + 1;
                ;
                m = m + 1;
            ;

            while (l < mid):
                aux[m] = sortedList[l];
                l = l + 1;
                m = m + 1;
            ;

            while (r < right):
                aux[m] = sortedList[r];
                r = r + 1;
                m = m + 1;
            ;

            i = i + 2 * width;
        ;


        i = 0;
        while (i < n_len):
            sortedList[i] = aux[i];
            i = i + 1;
        ;

        width = width * 2;
    ;

    return sortedList;
;

List[INT] unsorted = [3, 400, 32432, 46, 223, 9];
List[INT] sorted = mergeSort(unsorted);
show("Lista ordenada con Merge Sort:");
show(sorted);
